////
; Copyright (c) uib GmbH (www.uib.de)
; This documentation is owned by uib
; and published under the german creative commons by-sa license
; see:
; https://creativecommons.org/licenses/by-sa/3.0/de/
; https://creativecommons.org/licenses/by-sa/3.0/de/legalcode
; english:
; https://creativecommons.org/licenses/by-sa/3.0/
; https://creativecommons.org/licenses/by-sa/3.0/legalcode
;
; credits: http://www.opsi.org/credits/
////


:Author:    uib GmbH
:Email:     info@uib.de
:Date:      15.10.2023
:Revision:  4.3
:toclevels: 6
:doctype:   book
:icons:     font
:xrefstyle: full

include::common:partial$opsi_terms.adoc[]

[[opsi-script-linux]]
= *opsi-script* unter Linux und macOS

[[opsi-script-linux-intro]]

Seit Version 4.11.4 steht `opsi-script` auch unter Linux zur Verfügung.

Seit Version 4.12.1 steht `opsi-script`  auch unter macOS zur Verfügung.

Bedingt durch den Fortgang der Portierung und durch die Unterschiede der Betriebssysteme stehen bestimmte Funktionalitäten nicht unter allen Betriebssystemen zur Verfügung. Im folgenden sind entsprechende Abschnitte markiert:

* [W/L/M] sowohl unter Windows, Linux als auch unter macOS verfügbar

* [W] nur unter Windows verfügbar

* [L] nur unter Linux verfügbar

* [M] nur unter macOS verfügbar

[[opsi-script-linux-importantdiff]]
== Wichtige Unterschiede und Hinweise

'opsi-script.exe' ist unter Windows ein GUI Programm, welches über den Parameter `/silent` auch ohne GUI gestartet werden kann.

'opsi-script' unter Linux und macOS ist ein Kommandozeilen Programm, welches auch ohne Zugriff auf ein Display gestartet werden kann.
Es prüft aber automatisch ob der Zugriff auf ein grafisches Display möglich ist
und startet wenn möglich die grafische Variante 'opsi-script-gui'. Dies kann über den Parameter `-silent` unterdrückt werden.

'opsi-script-gui' ist eine GUI Version, welche sich auch nicht ohne grafisches Display starten lässt.

Unter Linux und macOS ist das Parameterzeichen nicht "/" sondern "-". Also statt unter Windows `opsi-script /help` hier `opsi-script -help`.


[[opsi-script-linux-path]]
== Pfade unter Linux

Unter Linux sind die unterschiedlichen Komponenten gemäß des Linux Filesystem Hierachie Standard nicht an einer Stelle zu finden. Daher hier ein Überblick:

Seit opsi-client-agent 4.2 liegen all Komponenten von opsi-script unter `/opt/opsi-script/`.

Vor opsi-client-agent 4.2:
* Ausführbare Programme: +
`/usr/bin/opsi-script` +
`/usr/bin/opsi-script-nogui`

* Language files: +
`/usr/share/locale`

* Skin files: +
Default = `/usr/share/opsi-script/skin` +
Custom = `/usr/share/opsi-script/customskin`

* opsi-script library files: +
`/usr/share/opsi-script/lib` +

Unabhängig von der Version gilt:

* Verzeichnisse für Logdateien: +
Ausgeführt als root: `/var/log/opsi-script` +
Ausgeführt als user: `/tmp`

* Config files: +
`/etc/opsi-script`

[[opsi-script-macos-path]]
== Pfade unter macOS

Unter macOS liegen all Komponenten von opsi-script unter `/Applications/opsi-script/`.

* Verzeichnisse für Logdateien: +
Ausgeführt als root: `/var/log/opsi-script` +
Ausgeführt als user: `/tmp`

* Config files: +
`/etc/opsi-script`

[[opsi-script-linux-pathhandling]]
== Verwendung von Pfaden in opsiscript

Seit Version 4.11.4 wird bei allen Funktionen welche einen Pfad erwarten, intern eine Funktion aufgerufen welchen den übergebenen String in einen für das Betriebssystem gültigen Pfad wandeln. Es werden also alle Pfadtrennzeichen korrekt gesetzt. So wird z.B. aus dem Pfad `/home/opsiproduct\myproduct\CLIENT_DATA` unter Linux `/home/opsiproduct/myproduct/CLIENT_DATA`. Dies bedeutet auch, dass unter Linux keine Dateien angelegt oder verarbeitet werden können welche einen Backslash im Namen haben.

[[opsi-script-linux-functions]]
== Linuxspezifische Funktionen

Zur Unterstützung von Linux gibt es folgende Linuxspezifische Funktionen:

* `GetOS`  // 'Linux' or 'Windows_NT'   [W/L/M]   xref:prim-section#GetOS[GetOS]

* `getLinuxDistroType`  // 'debian' or 'redhat' or 'suse' [L]  xref:prim-section#getLinuxDistroType[getLinuxDistroType]

* `getLinuxVersionMap` [L]  xref:prim-section#getLinuxVersionMap[getLinuxVersionMap]

* `chmod` in Files Sektionen [L/M]  xref:sec-section#chmod[chmod]

* `waitForPackageLock(`<wait_seconds>,<abort_on_timeout>`) : bool` xref:prim-section#waitForPackageLock[waitForPackageLock]

In den folgenden Kapiteln werden spezielle opsi Linux Befehle zur Installation von Software vorgestellt, welche aus der opsi-script Library `uib_lin_install` stammen. Diese Dokumentation ist in Englisch, da sie direkt aus dem Quellcode automatisch generiert wurde.

Zum Verständnis zunächst ein Überblick über die unterschiedlichen Ansätze der Methoden:

* Distributionsunabhängige Methoden:

** `cleanupPackageSystem`

** `installupdates`

* Installation von einem oder mehreren Paketen aus online Repos für eine spezifische Distribution +
Soll nur ein Paket installiert werden, so ist in dem Aufrufen statt $packagelist$, zu verwenden: `createStringList(<package name>)` +
Die Paketnamen in der Liste müssen zur Distribution / Version passen.

** debinstall($packagelist$ : stringlist) : string //since 4.12.4 [L]

** redinstall($packagelist$ : stringlist) : string //since 4.12.4 [L]

** suseinstall($packagelist$ : stringlist) : string //since 4.12.4 [L]

** ucsinstall($packagelist$ : stringlist) : string //since 4.12.4 [L]

* Installation / Deinstallation von einem oder mehren Paketen für eine bekannte Distribution / Version (d.h. Paketnamen müssen passen). +
Der notwendige Befehl wird anhand der Distribution ermittelt.

** genericLinInstall($packagelist$ : stringlist) : string

** linuxRemoveOnePackage($packagename$ : string) : string

** linuxInstallOneFile($packagefile$ : string) : string


* Installation / check / deinstallation von einem Paket aus online Repos für unterschiedliche Distributionen / Versionen, weswegen das Paket auch unterschiedliche Namen haben kann. +
D.h. es wird davon ausgegangen, das die Paketnamen in der Liste alles pseudonyme für das selbe Paket sind aber für unterschiedliche Versionen bzw. Distributionen.
Der notwendige Befehl wird anhand der Distribution ermittelt.

** linuxInstallOneOf($packagelist$ : stringlist) : string

** isOneInstalled($packagelist$ : stringlist) : string

** linuxRemoveOneOf($packagelist$ : stringlist) : string


Die Details zu den genannten Befehlen der Library finden sich hier: xref:libraries.adoc#Doc_fileuib_lin_install.opsiscript[Documentation of opsi library]: `uib_lin_install.opsiscript`

////
// http://www.methods.co.nz/asciidoc/userguide.html#X90
:leveloffset: 3

// Include os-lib_uib_Linuxinstalllib.asciidoc
include::../../en/common/os-lib_uib_lin_install.asciidoc[]

:leveloffset: 0
////

Verweise auf diese und weitere Linux-spezifische Bibliotheks Funktionen finden Sie hier: xref:reference-card.adoc#opsi-script-rc-linux-specific[Linux specific functions]



[[opsi-script-linux-examples]]
== Beispiel Scripte für Linux

[[opsi-script-linux-examples_linuxonly]]
=== Nur unter Linux ausführen

[source,opsiscript]
----
[Actions]
DefVar $OS$

set $OS$ = GetOS

if not ($OS$ = "Linux")
	logError "Installation aborted: wrong OS version: only Linux allowed"
	isFatalError "wrong OS"
endif
----

[[opsi-script-linux-examples_linuxversion]]
=== Welche Linux Version

[source,opsiscript]
----
[Actions]
DefVar $distCodeName$
DefVar $distroName$
DefVar $distRelease$
DefVar $distrotype$


DefStringList $linuxInfo$

set $distrotype$ = getLinuxDistroType
set $linuxInfo$ = getLinuxVersionMap
set $distCodeName$ = getValue("Codename", $linuxInfo$)
set $distRelease$ = getValue("Release", $linuxInfo$)
set $distroName$  = getValue("Distributor ID", $linuxInfo$)
----

.getLinuxVersionMap Result Examples
[options="header"]
|==========================
| Distro | Distributor ID | Release | Codename | Description
|Ubuntu Focal| Ubuntu | 20.04 | focal |
|Debian 8 | Debian | 8.3 | jessie | Debian GNU/Linux 8.3 (jessie)
|openSUSE Leap 42.1 | SUSE LINUX | 42.1 | n/a | openSUSE Leap 42.1 (x86_64)
|SLES12SP1 | SUSE LINUX | 12.1 | n/a | SUSE Linux Enterprise Server 12 SP1
|CentOS 7.0 | CentOS | 7.0.1406 | Core | CentOS Linux release 7.0.1406 (Core)
|RedHat 7.0 | RedHatEnterpriseServer | 7.0 | Maipo | Red Hat Enterprise Linux Server release 7.0 (Maipo)
|UCS 4.1 | Univention | 4.1-1 errata122 | Vahr | Univention Corporate Server 4.1-1 errata122 (Vahr)
|==========================


[[opsi-script-linux-examples_shellinanicon]]
=== ShellInAnIcon Aufruf

[source,opsiscript]
----
[Actions]

ShellInAnIcon_ls

[ShellInAnIcon_ls]
set -x
ls
exit $?
----

Es ist oft hilfreich mit `set -x` anzufangen, damit der Skriptablauf in der Logdatei erkennbar ist. Die letzte Zeile sollte `exit $?` sein, damit zumindest der letzte exitcode der Sektion übergeben wird.

[[opsi-script-linux-examples_addrepo]]
=== Hinzufügen eines Linux Paket Repositories

.Ubuntu / Debian

[source,opsiscript]
----
[Actions]
DefVar $newrepo$

set $newrepo$ = "deb http://download.opensuse.org/repositories/home:/uibmz:/opsi:/opsi40/Debian_7.0/ ./"

comment "Method 1: use add-apt-repository ..."
ShellInAnIcon_add_rep_deb
ShellInAnIcon_add_repokey_deb
comment "Method 2: use add-apt-repository ..."
PatchTextFile_add_repo_deb "/etc/apt/sources.list"
ShellInAnIcon_add_repokey_deb

[ShellInAnIcon_add_rep_deb]
set -x
export export DEBIAN_FRONTEND=noninteractive
apt-get --yes --force-yes install software-properties-common
apt-get --yes --force-yes install python-software-properties
add-apt-repository '$newrepo$'
exit $?

[PatchTextFile_add_repo_deb]
FindLine_StartingWith "$newrepo$"
DeleteTheLine
GoToBottom
InsertLine "$newrepo$"

[ShellInAnIcon_add_repokey_deb]
set -x
export wget --no-check-certificate -O - $newrepo$/Release.key | apt-key add -
apt-get update
exit $?
----

.SUSE

[source,opsiscript]
----
[Actions]
DefVar $newrepo$

set $newrepo$ = "http://download.opensuse.org/repositories/home:/uibmz:/opsi:/opsi40/openSUSE_13.1/home:uibmz:opsi:opsi40.repo"

ShellInAnIcon_add_opsi_repository_suse

[ShellInAnIcon_add_opsi_repository_suse]
set -x
export zypper --no-gpg-checks --non-interactive --gpg-auto-import-keys ar --refresh $newrepo$
zypper --no-gpg-checks --non-interactive --gpg-auto-import-keys refresh
exit $?
----

.CentOS / Redhat

[source,opsiscript]
----
[Actions]
DefVar $newrepo$

set $newrepo$ = "http://download.opensuse.org/repositories/home:/uibmz:/opsi:/opsi40/CentOS_7/home:uibmz:opsi:opsi40.repo"

comment "Method 1: use wget ..."
ShellInAnIcon_add_repo_redhat
ShellInAnIcon_refresh_repo_redhat
comment "Method 2: use PatchTextFile ..."
PatchTextFile_add_repo_redhat "/etc/yum.repos.d/mynew.repo"
ShellInAnIcon_refresh_repo_redhat

ShellInAnIcon_add_repo_redhat

[ShellInAnIcon_add_repo_redhat]
set -x
export yum -y install wget
cd /etc/yum.repos.d
wget --no-check-certificate $newrepo$
exit $?

[PatchTextFile_add_repo_redhat]
AppendLine "[home_uibmz_opsi_opsi40]"
AppendLine "name=opsi 4.0 (CentOS_7)"
AppendLine "type=rpm-md"
AppendLine "baseurl=http://download.opensuse.org/repositories/home:/uibmz:/opsi:/opsi40/CentOS_7/"
AppendLine "gpgcheck=1"
AppendLine "gpgkey=http://download.opensuse.org/repositories/home:/uibmz:/opsi:/opsi40/CentOS_7/repodata/repomd.xml.key"
AppendLine "enabled=1"

[ShellInAnIcon_refresh_repo_redhat]
set -x
yum makecache
yum -y repolist
exit $?
----

[[opsi-script-linux-examples_delrepo]]
=== Löschen eines Repositories

.Ubuntu / Debian

[source,opsiscript]
----
[Actions]
DefVar $delrepo$
DefStringlist = $resultlist$

set $delrepo$ = "deb http://download.opensuse.org/repositories/home:/uibmz:/opsi:/opsi40/Debian_7.0/ ./"

if LineBeginning_ExistsIn($delrepo$, "/etc/apt/sources.list")
	PatchTextFile_del_repo_deb  "/etc/apt/sources.list"
	set $resultlist$ = shellCall("apt-get update")
endif

[PatchTextFile_del_repo_deb]
FindLine_StartingWith "$delrepo$"
DeleteTheLine
----


.SUSE

[source,opsiscript]
----
[Actions]
DefVar $delrepo$

comment "$delrepo$ is the section name of the repo file in /etc/zypp/repos.d/"
comment "$delrepo$ can be found by zypper lr"
set $delrepo$ = "home_uibmz_opsi_opsi40"
ShellInAnIcon_del_opsi_repository_suse

[ShellInAnIcon_del_opsi_repository_suse]
set -x
export PATH=/usr/local/bin:/usr/bin:/bin:/usr/local/sbin:/usr/sbin:/sbin
zypper --non-interactive rr  $delrepo$
exit $?
----

.CentOS / Redhat

[source,opsiscript]
----
[Actions]
DefVar $delrepo$

comment "$delrepo$ ist the name of the repo file in /etc/yum.repos.d"
set $delrepo$ = "/etc/yum.repos.d/home:uibmz:opsi:opsi40.repo"

[ShellInAnIcon_del_opsi_repository_redhat]
set -x
export PATH=/usr/local/bin:/usr/bin:/bin:/usr/local/sbin:/usr/sbin:/sbin
rm $delrepo$
yum makecache
yum -y repolist
exit $?
----

[[opsi-script-linux-install_package]]
=== Installieren eines Paketes

.Generic for all supported distributions

A simple example:

[source,opsiscript]
----
[Actions]
importlib "uib_lin_install"

DefStringlist $packages$
DefVar $installresult$

comment "install new needed packages"
if waitForPackageLock("300", "false")
	comment "we got the package lock."
else
	LogError "could not get Package Lock"
endif

set $packages$ = CreateStringlist("lsb-release","cifs-utils","xterm")
set $installresult$ = genericLinInstall($packages$)
if not(stringtobool($installresult$))
	LogError "failed install packages"
	Message "failed install packages"
	isFatalError "failed dependent packages"
endif
----

A more sophisticated example:

[source,opsiscript]
----
[Actions]
importlib "uib_lin_install"

DefStringlist $packages$
DefVar $installresult$
DefStringlist $errorList$
DefVar $fatal_error$
DefVar $result_string$

if waitForPackageLock("300", "false")
	comment "we got the package lock."
else
	LogError "could not get Package Lock"
endif

comment "update and clean package system"
cleanupPackageSystem()

comment "install pending updates"
set $result_string$ = installupdates()

comment "install new needed packages"
set $packages$ = CreateStringlist("lsb-release","cifs-utils","xterm")
set $installresult$ = genericLinInstall($packages$)
if not(stringtobool($installresult$))
	if waitForPackageLock("300", "false")
		comment "we got the package lock."
	else
		LogError "could not get Package Lock"
	endif
	cleanupPackageSystem()
	set $installresult$ = genericLinInstall($packages$)
	if not(stringtobool($installresult$))
		LogError "failed install packages"
		Message "failed install packages"
		;isFatalError "failed install packages"
		set $fatal_error$ = "true"
		set $errorList$ = addtolist($errorList$, " failed install packages")
	endif
endif
----

////
.Ubuntu / Debian

[source,opsiscript]
----
[Actions]

ShellInAnIcon_install_wget_debian

[ShellInAnIcon_install_wget_debian]
set -x
export PATH=/usr/local/bin:/usr/bin:/bin:/usr/local/sbin:/usr/sbin:/sbin
export DEBIAN_FRONTEND=noninteractive
apt-get --yes --force-yes install wget
exit $?
----

Für Debian / Ubuntu sind die Zeilen `DEBIAN_FRONTEND=noninteractive` und die apt-get Optionen `--yes  -o Dpkg::Options::="--force-confdef" -o Dpkg::Options::="--force-confold` nötig um eine vollständig nicht interaktive Installation zu gewährleisten.

.SUSE

[source,opsiscript]
----
[Actions]

ShellInAnIcon_install_wget_suse

[ShellInAnIcon_install_wget_suse]
set -x
export PATH=/usr/local/bin:/usr/bin:/bin:/usr/local/sbin:/usr/sbin:/sbin
zypper --no-gpg-checks --non-interactive install wget
exit $?
----
Die `zypper`  Optionen `--no-gpg-checks --non-interactive` sind nötig um eine vollständig nicht interaktive Installation zu gewährleisten.

.CentOS / Redhat

[source,opsiscript]
----
[Actions]

ShellInAnIcon_install_wget_redhat

[ShellInAnIcon_install_wget_redhat]
set -x
export PATH=/usr/local/bin:/usr/bin:/bin:/usr/local/sbin:/usr/sbin:/sbin
yum -y install wget
exit $?
----
Die `yum`  Option `-y` ist nötig um eine vollständig nicht interaktive Installation zu gewährleisten.

[[opsi-script-linux-examples_integrated_example]]
=== Integriertes Beispiel

[source,opsiscript]
----
[Actions]

DefVar $OS$
DefVar $distro_type$

DefStringlist $list$


set $OS$ = GetOS

if not ($OS$ = "Linux")
	logError "Installation aborted: wrong OS version: only Linux allowed"
	isFatalError "wrong OS"
endif

set $distro_type$ = getLinuxDistroType
set $list$ = getLinuxVersionMap

comment "install wget ...."
if $distro_type$ = "redhat"
	ShellInAnIcon_install_wget_redhat
else
	if $distro_type$ = "suse"
		ShellInAnIcon_install_wget_suse
	else
		if $distro_type$ = "debian"
			ShellInAnIcon_install_wget_debian
		else
			LogError "Unknown distro type"
		endif
	endif
endif

[ShellInAnIcon_install_wget_debian]
set -x
export PATH=/usr/local/bin:/usr/bin:/bin:/usr/local/sbin:/usr/sbin:/sbin
export DEBIAN_FRONTEND=noninteractive
apt-get --yes --force-yes install wget
exit $?

[ShellInAnIcon_install_wget_redhat]
set -x
export PATH=/usr/local/bin:/usr/bin:/bin:/usr/local/sbin:/usr/sbin:/sbin
yum -y install wget
exit $?

[ShellInAnIcon_install_wget_suse]
set -x
export PATH=/usr/local/bin:/usr/bin:/bin:/usr/local/sbin:/usr/sbin:/sbin
zypper --no-gpg-checks --non-interactive install wget
exit $?
----
////

[[opsi-script-macos-functions]]
== macOS spezifische Funktionen

Für die Unterstützung von macOS gibt es die folgenden spezifischen Funktionen:

* `GetOS`  // 'Linux' or 'Windows_NT' or 'macOS'  [W/L/M] xref:prim-section#GetOS[GetOS] +

* `getMacosVersionInfo`   [M] xref:prim-section#getMacosVersionInfo[getMacosVersionInfo]

* `getMacosVersionMap` [M] xref:prim-section#getMacosVersionMap[getMacosVersionMap]

* `getOSArchitecture` // 'x86_32' or 'x86_64' or 'arm_64'  [W/L/M] xref:prim-section.adoc#getOSArchitecture[OSArchitecture]

* `chmod` in Files sections [L/M] xref:sec-section#chmod[chmod]

* `importlib "uib_macosinstalllib"`  :

* `install_macos_app($myapp$ : string) : string`  [M] +

* `install_macos_pkg($mypkg$ : string) : string`  [M] +

* `install_macos_dmg($mydmg$ : string) : string`  [M] +

* `install_macos_zip($myzip$ : string) : string`  [M] +

* `install_macos_generic($myfile$ : string) : string`  [M]


Die Details zu den genannten Befehlen der Library finden sich hier: xref:libraries#Doc_fileuib_macosinstalllib.opsiscript[Documentation of opsi library: `uib_macosinstalllib.opsiscript`]


////
// http://www.methods.co.nz/asciidoc/userguide.html#X90
:leveloffset: 3

// Include os-lib_uib_Linuxinstalllib.asciidoc
include::../../en/common/os-lib_uib_macosinstalllib.asciidoc[]

:leveloffset: 0
////

Verweise auf weitere macOS-spezifische Bibliotheks Funktionen finden Sie hier: xref:reference-card#opsi-script-rc-macos-specific[macOS specific functions]

[[opsi-script-macos-examples]]
== Beispiel Scripte für macOS

[[opsi-script-macos-examples_macoonly]]
=== Nur auf macOS ausführen

[source,opsiscript]
----
[Actions]
DefVar $OS$

set $OS$ = GetOS

if not ($OS$ = "macOS")
	logError "Installation aborted: wrong OS version: only macOS allowed"
	isFatalError "wrong OS"
endif
----

[[opsi-script-macos-examples_macOSversion]]
=== Welche macOS Version

Der code:

[source,opsiscript]
----
Set  $macOSinfomap$ = getMacosVersionMap
----
ergibt (zum Beispiel) folgenden log:
----
The value of the variable "$macOSinfomap$" is now:
(string   0)Release=11.0
(string   1)Build=20A5364e
(string   2)kernel name=Darwin
(string   3)node name=vmmac1100onmm1.uib.local
(string   4)kernel release=20.1.0
(string   5)kernel version=Darwin Kernel Version 20.1.0: Fri Aug 28 20:45:30 PDT 2020; root:xnu-7195.40.65.0.2~61/RELEASE_X86_64
(string   6)machine=x86_64
(string   7)processor=i386
(string   8)operating system=macOS
----
