////
; Copyright (c) uib GmbH (www.uib.de)
; This documentation is owned by uib
; and published under the german creative commons by-sa license
; see:
; https://creativecommons.org/licenses/by-sa/3.0/de/
; https://creativecommons.org/licenses/by-sa/3.0/de/legalcode
; english:
; https://creativecommons.org/licenses/by-sa/3.0/
; https://creativecommons.org/licenses/by-sa/3.0/legalcode
;
; credits: http://www.opsi.org/credits/
////

:Author:    uib GmbH
:Email:     info@uib.de
:Date:      04.10.2023
:Revision:  4.3
:toclevels: 6
:doctype:   book
:icons:     font
:xrefstyle: full

include::common:partial$opsi_terms.adoc[]

[[opsi-setup-detector-support_custom_directory]]
=== *opsi-setup-detector*: Custom-Verzeichnis

Auf dem Reiter _Produkt Konfiguration 1_ sehen Sie im unteren Bereich die Checkbox _Unterstütze custom directory_. Aktivieren Sie diese, und das Produkt erhält ein zusätzliches Verzeichnis namens `custom`. Hier können kundenspezifische Anpassungen landen -- bei einem Upgrade auf eine neue Paketversion bleibt das vorhandene Verzeichnis `custom` unverändert. Der Code enthält Vorlagen, um Dateien aus diesem Verzeichnis in die Installation einzufügen. 

In die Datei `setup.opsiscript` wird Folgendes eingefügt:

[source,ini]
----
; copy custom files to install dir
Files_copy_from_custom_dir
----

Das ruft eine `Files`-Sektion auf, um Dateien aus einem `custom`-Verzeichnis auf den Client zu kopieren. Die dazugehörige Sektion sieht so aus:

[source,ini]
----
[Files_copy_from_custom_dir]
copy -s "%scriptPath%\custom\*" "$installdir$"
----

In diesem Beispiel wird der Inhalt das `custom`-Verzeichnisses in das Installations-Verzeichnis kopiert. Außerdem wird das `custom`-Verzeichnis selbst erzeugt. Es erhält die beiden Dateien `OPSI\preinst` und `OPSI\postinst`. Sie sorgen dafür, dass der Inhalt des Verzeichnisses auf dem Depot auch bei Aktualisierungen erhalten bleibt.

[[opsi-setup-detector-install_from_local_temp_dir]]
=== *opsi-setup-detector*: Lokales, temporäres Verzeichnis

Der `opsi-setup-detector` hat auf dem Reiter _Produkt Konfiguration 1_ die Checkbox _Installiere von lokalem, temporären Verzeichnis_. Aktivieren Sie diese, und die Installations-Dateien werden zunächst in ein lokales, temporäres Verzeichnis kopiert und dann aus diesem heraus installiert. Das Feature bietet sich beispielsweise an, wenn eine Installation die Netzwerkverbindung beeinträchtigen könnte, z.{nbsp}B. bei Treibern.

Der `opsi-setup-detector` erzeugt dann ein zusätzliches boolesches Property `install_from_local_tmpdir`, das in der Voreinstellung auf `false` steht und diese Beschreibung erhält: _Bestimmt, ob die Installationsdateien nach lokal kopiert werden_.

In die Datei `setup.opsiscript` wird Folgendes eingefügt:

[source,ini]
----
set $Install_from_local_tmpdir$ = GetProductProperty("Install_from_local_tmpdir","False")

; before the installation:
if $Install_from_local_tmpdir$ = "true"
	; copy installer files to tempdirectory
	Files_copy_installer_to_temp
	; set $installerSourceDir$ to tmpdir
	set $installerSourceDir$ = forcePathDelims("%opsiTmpDir%\"+$ProductId$)
endif

; do the installation

; after the installation
if $Install_from_local_tmpdir$ = "true"
	; cleanup installer tempdirectory
	Files_del_installer_from_temp
endif
----

Jetzt werden die Installations-Dateien vorübergehend in ein temporäres Verzeichnis verschoben und nach der Installation gelöscht, wenn die Bedingung `Install_from_local_tmpdir` auf `true` gesetzt ist.

Die dazugehörigen Sektionen sehen so aus: 

[source,ini]
----
; copy installer files to tempdirectory
[Files_copy_installer_to_temp]
copy -s "$installerSourceDir$\*.*" "%opsiTmpDir%\$ProductId$"

; cleanup installer tempdirectory
[Files_del_installer_from_temp]
del -sf "%opsiTmpDir%\$ProductId$\"
----

[[opsi-setup-detector-handle_license_key]]
=== *opsi-setup-detector*: Lizenzschlüssel

Der `opsi-setup-detector` enthält auf dem Reiter _Produkt Konfiguration 1_ die Checkbox _Behandle Lizenzkeys_. Wenn Sie diese aktivieren, werden Property und Code zur Behandlung von Lizenzschlüsseln hinzugefügt.

Es wird ein zusätzliches Property namens `secretlicense_or_pool` erzeugt; es ist in der Voreinstellung leer und trägt diese Beschreibung: _Lizenzkey oder opsi Lizenzpool_. Das Schlüsselwort `secret` im Property-Namen führt dazu, dass im `opsi-configed` der Wert des Propertys nur maskiert angezeigt wird.

In die Datei `setup.opsiscript` wird Folgendes eingefügt:

[source,ini]
----
DefVar $LicenseHandledByScript$ = "true"

set $LicenseOrPool$ = GetConfidentialProductProperty("SecretLicense_or_Pool","")

; before the installation:
; ---------------------------------------------------------------
comment "handle license "
; ----------------------------------------------------------------
if $LicenseHandledByScript$ = "true"
	comment "we should handle license"
	;reserve license and get license key
	set $LicenseKey$ = get_licensekey_byPoolOrKey($LicenseOrPool$)
	; here the section or function calls should follow which
	; make use of the license key resp the license reservation
endif
----

Der Aufruf von `get_licensekey_byPoolOrKey` prüft, ob der Wert des Propertys der Name eines Lizenzpools aus der opsi-Erweiterung xref:opsi-modules:licensemanagement.adoc[Lizenzmanagement] ist. Falls ja, so wird ein Lizenzschlüssel aus diesem Pool bezogen. Falls nein, so wird der Property-Inhalt als Lizenzschlüssel geliefert.

In die Datei `uninstall.opsiscript` wird Folgendes eingefügt:

[source,ini]
----
DefVar $LicenseHandledByScript$ = "true"

set $LicenseOrPool$ = GetConfidentialProductProperty("SecretLicense_or_Pool","")

; after the uninstallation

; ---------------------------------------------------------------
comment "handle license "
; ----------------------------------------------------------------
if $LicenseHandledByScript$ = "true"
	comment "we should free license used"
	Set $tmpstr$ = FreeLicense($LicenseOrPool$)
endif
----

[[opsi-setup-detector-desktopicon]]
=== *opsi-setup-detector*: Desktop-Icon

Der `opsi-setup-detector` enthält auf dem Reiter _Produkt Konfiguration 1_ die Checkbox _DesktopIcon_. Aktivieren Sie diese, um Property und Code zur Behandlung von Desktopsymbolen hinzuzufügen.

Es wird dann ein zusätzliches boolesches Property namens `desktopicon` erzeugt, in der Voreinstellung steht es auf `false` und hat diese Beschreibung: _Soll es ein Desktopicon geben?_.

In die Datei `setup.opsiscript` wird folgender Code eingefügt: 

[source,ini]
----
set $desktopicon$ = GetProductProperty("desktopicon", "False")


; after the installation
; handle desktop icon
if $DesktopIcon$ = "true"
	comment "Create Desktop Icon"
	Linkfolder_create_desktop_icon
else
	comment "Remove Desktop Icon"
	Linkfolder_remove_desktop_icon
endif
----

Falls `desktopicon` auf `true` steht, wird eine Sektion `Linkfolder` aufgerufen. Der dazugehörige Code (der auch in die Datei `uninstall.opsiscript` eingefügt wird) sieht so aus:

[source,ini]
----
[Linkfolder_remove_desktop_icon]
; check delete_element
set_basefolder common_desktopdirectory
set_subfolder ""
delete_element $productId$

[Linkfolder_create_desktop_icon]
; check name, target and working_dir
set_basefolder common_desktopdirectory
set_subfolder ""
set_link
	name: $productId$
	target: $Installdir$\$targetprogram$
	parameters:
	working_dir: $Installdir$
	icon_file:
	icon_index:
end_link
----

In die Datei `delinc.opsiinc` werden außerdem diese beiden Zeilen eingefügt:

[source,ini]
----
comment "Start Remove Desktop Icon Handling :"
Linkfolder_remove_desktop_icon
----

[[opsi-setup-detector-customize_profile]]
=== *opsi-setup-detector*: Lokale Benutzerprofile anpassen

Der `opsi-setup-detector` enthält auf dem Reiter _Produkt Konfiguration 1_ die Checkbox _Customize Profile_. Aktivieren Sie diese, und der Code wird ergänzt um eine Sektion `Profileactions`. Sie nimmt Anpassungen an den lokalen Benutzerprofilen vor. Diese Funktionalität wird auch über ein Loginskript für _Roaming Profiles_ bereitgestellt.

Über die Datei `OPSI/control` wird das `setup.opsiscript` nicht nur als `setupScript`, sondern auch als `userLoginScript` bereitgestellt.

In die Datei `setup.opsiscript` wird Folgendes eingefügt:

[source,ini]
----
; Run the customization for user profiles
ProfileActions
----

Der Code ruft eine `ProfileActions`-Sektion auf. Diese wird je nach Aufruftyp für alle lokalen Profile oder für den gerade eingeloggten Benutzer ausgeführt (siehe auch Kapitel xref:opsi-modules:user-profile.adoc[User Profile Management]).

Die dazugehörigen Sektionen, die als Templates für Aufrufe zur Manipulation der User Profiles dienen, sehen so aus:

[source,winst]
----
[ProfileActions]
; all section that called from [ProfileActions]
; will be executed for all user profiles
;
; if this script runs as loginscript
; only the [ProfileActions] will be executed

; copy some files to every user profile
Files_copy_to_user_profiles

; make entries in every currentuser hive
Registry_current_user

; modify or create ini files in all user profiles
;Patches_in_user_profiles  "%UserProfileDir%\Appdata\Roaming\<path_to_ini_file>"
Patches_in_user_profiles  "%UserProfileDir%\Appdata\Roaming\osd_profile_example\osd_profile_example.ini"

[Files_copy_to_user_profiles]
; example structure:
;copy "%Scriptpath%\profile_files\*.*" "%UserProfileDir%\Appdata\Roaming\<path_to_application_dir>"
; example:
;copy "%Scriptpath%\profile_files\*.*" "%UserProfileDir%\Appdata\Roaming\osd_profile_example"

[Registry_current_user]
; example structure:
;openkey [HKCU\Software\<application key>]
;set "<var name>" = "<var value>"
; example:
;openkey [HKCU\Software\osd_profile_example]
;set "osd_profile_example_entry" = "example_value"

[Patches_in_user_profiles]
; example structure:
; set [<section name>] <key name>=<value>
; example:
;set [example_section] example_key=example_value
----

