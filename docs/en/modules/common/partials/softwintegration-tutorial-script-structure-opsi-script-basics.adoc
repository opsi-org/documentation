////
; Copyright (c) uib GmbH (www.uib.de)
; This documentation is owned by uib
; and published under the german creative commons by-sa license
; see:
; https://creativecommons.org/licenses/by-sa/3.0/de/
; https://creativecommons.org/licenses/by-sa/3.0/de/legalcode
; english:
; https://creativecommons.org/licenses/by-sa/3.0/
; https://creativecommons.org/licenses/by-sa/3.0/legalcode
;
; credits: http://www.opsi.org/credits/
////

:Author:    uib GmbH
:Email:     info@uib.de
:Date:      15.02.2024
:Revision:  4.3
:toclevels: 6
:doctype:   book
:icons:     font
:xrefstyle: full

include::common:partial$opsi_terms.adoc[]

[[opsi-softwintegration-tutorial-script-structure]]
=== Structure of an *opsi-script* Script

The following sections will detail the key components of an `opsi-script` script tailored for Windows computers. As an illustration, here is an example of a basic `opsi-script` script:

// Dieses Beispiel ist Bestandteil einer pädagogischen Abfolge
// Daher bitte nicht modifizieren
// ohne die Details mit den Trainern das abzusprechen
// auch wenn etwas veraltet ist
[source,ini]
----
[Actions]
WinBatch_tightvnc_silent_install

[WinBatch_tightvnc_silent_install]
"%ScriptPath%\tightvnc-1.3.9-setup.exe" /silent
----

An `opsi-script` script is structured into two main types of sections:  <<opsi-softwintegration-tutorial-primary-sections,primary>> and <<opsi-softwintegration-tutorial-secondary-sections,secondary>> sections. Similar to INI files, each section is introduced by its name enclosed in square brackets.

The core activities of software installation are carried out in the secondary sections, which are invoked by the primary sections. Each secondary section is designated for specific topics and follows a unique syntax: the section name starts with the type, followed by a user-defined name.

In the provided example, the primary section `[Actions]` triggers the secondary section `[WinBatch_tightvnc_silent_install]`; this section belongs to the `WinBatch` type. The contents of a `WinBatch` section are executed using the Windows API. Here, the `msiexec` tool is employed to install (`/i`) the 64 bit version of TightVNC silently (using the `/quiet` option). The `/norestart` option is included to prevent the computer from rebooting after the installation, which is a specific requirement for TightVNC.

[[opsi-softwintegration-tutorial-primary-sections]]
==== Primary Sections

* *Actions*: The `[Actions]` section is essentially the main program, marking the commencement of script processing.

* *Sub-Sections*: Sections of the program required to be executed multiple times can be organized into sub-sections. These sub-sections can also be externalized into separate files for ease of use and reference.

.Avoide duplicate Code via distributed Sub-sections.
image::opsi-winst-delsub-schema-en.png["Avoide duplicate Code via distributed Sub-sections.",width=800, pdfwidth=80%]

The primary sections, which function as the main programs, dictate the script's flow. In these sections, you can use variables, conditional statements, functions, and other programming constructs. For more detailed information on these elements, please refer to the section <<opsi-softwintegration-tutorial-elementary-commands>>.

//hier
[[opsi-softwintegration-tutorial-secondary-sections]]
==== Important secondary sections

Files::
File operations, such as:

* copy (with version control, recursive ...).
* delete
* create directories
* ...

WinBatch::
Used to call programs via the Windows API.
For example, invokes to setup programs in silent mode are made in these sections.

ShellScript::
The content of this section is passed to the operating system on the typical _shell_ for execution. This _shell_ is the `cmd.exe` for Windows, for Linux and for macOS the `bash`. As such, normal batch scripts can be stored here.

ExecWith::
The contents of these sections are passed to an external program such as an (interpreter) for execution.
For example, `ExecWith` can be used to integrate AutoIt scripts http://www.autoitscript.com directly into the opsi-script script.

Registry::
The _Registry sections_ are used to edit the registry.

LinkFolder::
_LinkFolder sections_ are used to create and remove shortcuts.
For example, such shortcuts can be created on the desktop or in the start menu.


[[opsi-softwintegration-tutorial-global-constants]]
==== Global constants

Global constants are text placeholders that can be used in primary and secondary sections
and are textually replaced by their values at runtime. +
The use of placeholders can be utilized to ensure
that paths are set correctly in different environments (in the case of systems with different languages or operating system versions for example).

Examples:

+%ProgramFiles32Dir%+:: c:\Program Files (x86)
+%Systemroot%+::        c:\windows
+%System%+::            c:\windows\system32
+%opsiTmpDir%+::       c:\opsi.org\tmp
+%Scriptpath%+::        <path to running script>


[[opsi-softwintegration-tutorial-second-example]]
==== Second example: tightvnc

For clarification purposes, now a simple script for the installation of _tightvnc_.
As a matter of fact this script would get on with the call of the silent installation in the _Winbatch section_.
However, during a repeated installation an interactive dialog appears here (because of a running service).
This dialog window is closed (if it appears) with the help of 'AutoIt'.

// Dieses Beispiel ist Bestandteil einer pädagogischen Abfolge
// Daher bitte nicht modifizieren
// ohne die Details mit den Trainern das abzusprechen
// auch wenn etwas veraltet ist
[source,winst]
----
[Actions]
Message "Installing tightvnc 1.3.9 ..."
ExecWith_autoit_confirm "%ScriptPath%\autoit3.exe" WINST /letThemGo
WinBatch_tightvnc_silent_install
KillTask "autoit3.exe"

[WinBatch_tightvnc_silent_install]
"%ScriptPath%\tightvnc-1.3.9-setup.exe" /silent

[ExecWith_autoit_confirm]
; Wait for the confirm dialog which only appears if tightvnc was installed before as service
; Waiting for the window to appear
WinWait("Confirm")
; Activate (move focus to) window
WinActivate("Confirm")
; Choose answer no
Send("N")
----

[[opsi-softwintegration-tutorial-elementary-commands]]
==== Elementary commands for primary sections

[[opsi-softwintegration-tutorial-elementary-commands-string-variable]]
===== String-Variable

Variable declaration:: 'DefVar <variable name> [= <initial value>]'

Variable assignment:: 'Set <variable name> = <value>'

*Example:*
[source,winst]
----
DefVar $ProductId$
Set $ProductId$ = "firefox"
----

or

[source,winst]
----
DefVar $ProductId$ = "firefox"
----

IMPORTANT: String variables are handled differently in primary and secondary sections.
In primary sections, string variables are independent objects.
Only here they can be declared and assigned values.
Accordingly, the connection of variables and strings to a string expression is to be performed with a `"+"` operator. +
Example: `"Installing "+ $ProductId$ +" ..."` +
In secondary sections, string variables are replaced with the contents of the variable before the section is executed. +
For example: `"Installing $ProductId$ ..."` +
This should be taken into consideration when the corresponding string expressions are cut and pasted in the script. +
The advantage of this construction is that in sections that are executed outside the 'opsi-script' (ShellScript / Execwith)
opsi-script variables can be manipulated at ease.

[[opsi-softwintegration-tutorial-elementary-commands-message]]
===== Message / ShowBitmap

Text output during installation: +
`Message <string>`

*Example:*
[source,winst]
----
Message "Installing "+ $ProductId$ +" ..."
----

To output a graphic during the installation: +
`ShowBitmap <filename> <subtitle>`

*Example:*
[source,winst]
----
ShowBitmap "%ScriptPath%\python.png" "Python"
----

[[opsi-softwintegration-tutorial-elementary-commands-if-else-endif]]
===== if [elseif] [else] endif

*Syntax:*
[source,winst]
----
if <condition>
	;statement(s)
[elseif <condition>
;statement(s)]
[
else
	;statement(s)
]
endif
----

[[opsi-softwintegration-tutorial-elementary-commands-functions]]
===== Functions

HasMinimumSpace:: Checks for free space on the hard disk.
FileExists:: Checks for the existence of a file or directory.

[[opsi-softwintegration-tutorial-elementary-commands-error]]
===== Errors, logging and comments

Comment characters ';':: Lines that start with a semicolon (';') are not interpreted.

Comment:: Writes a comment message to the logfile.

LogError:: Writes an error message to the logfile.

IsFatalError:: Cancels the execution of the running script and reports the installation as failed.

[[opsi-softwintegration-tutorial-elementary-commands-requirements]]
===== Condition for execution

requiredWinstVersion:: specifies the (minimum) required opsi-script version.

[[opsi-softwintegration-tutorial-winst-commands]]
===== Other important opsi-script functions

An overview of the opsi-script functions is given by the reference card +
https://docs.opsi.org/opsi-docs-en/4.2/opsi-script-manual/reference-card.html


A detailed documentation can be found in the opsi-script manual: +
https://docs.opsi.org/opsi-docs-en/4.2/opsi-script-manual/opsi-script-manual.html

Here are a few more notes on particularly important elements:

.Stringlists:
String lists are very powerful, especially for evaluating output from external programs. Read the opsi-script docs for more information.

.ExitWindows:
Reboot/Shutdown the system and finish the opsi-script.

*  `ExitWindows /Reboot` +
Computer restart after completion of the running script.

*  `ExitWindows /ImmediateReboot` +
Immediate reboot.

*  `ExitWindows /ImmediateLogout` +
Immediately stop script editing and terminate opsi-script.

.Product-Properties:
For some products it's necessary to provide options.
These are specifically evaluated per client at runtime.
How such properties are created is described in the chapter
<<opsi-client-softwintegration-create-opsi-package,Creation of opsi product packages>>

The access to the values of the properties is done via the function `GetProductProperty`:

[source,winst]
----
if GetProductProperty("example-property", "no") = "yes"
	Files_copy_extra_files
endif
----

.Encoding:
Write your scripts in UTF-8 encoding and set the line +
`encoding=utf8`
At the beginning of the file
