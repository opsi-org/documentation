//// 
; Copyright (c) uib gmbh (www.uib.de)
; This documentation is owned by uib
; Until we found a better license:
; All rights reserved.
; credits: http://www.opsi.org/credits/
////

:Author:    uib gmbh
:Email:     info@uib.de
:Date:      06.07.2011
:Revision:  4.0.1
:toclevels: 6

include::../common/opsi_terms.asciidoc[]


[[opsi-manual-ci]]
== Adapting the opsi-client-agent to your Corporate Identity (CI)

Adapting the '{opsi-client-agent}' to your Corporate Identity (CI) will enhance the acceptance of a opsi roll out.

It is possible to manipulate the graphical skin of the '{opsi-winst}' by changing the file `bg.png` in the sub directory `winstskin`. After any change at this file in the directory `/opt/pcbin/install/opsi-client-agent/files/opsi/opsi-winst/winstskin` you should execute the command 
[source,prompt]
----
touch /opt/pcbin/install/opsi-client-agent/files/opsi/opsi-winst/winst32.exe 
----
which changes the time-stamp of this file. As result of this operation the '{opsi-client-agent}' will see a 'changed' '{opsi-winst}' at the next startup and copy the '{opsi-winst}' and all it files (including `bg.png`) to the client.

In addition you will find at the directory
`/opt/pcbin/install/opsi-client-agent/files/opsi/dist/notifier`
the files `action.bmp` and `event.bmp` which may be changed to fit your preferences. These files will be transferred to the client at the installation of the '{opsi-client-agent}'.

You may may change location, color and font of the labels which will be displayed at run time by changing the Ini files of the notifier (`event.ini`, `action.ini`, `shutdown.ini`). Please note that these files will be overwritten in case of installing a new version of the '{opsi-client-agent}' at the server.

The changes may also be done by a '{opsi-winst}' script which can be used as stand-alone product or as update script of the '{opsi-client-agent}'. 

Here a example:

[source,winst]
----
[Actions]
requiredWinstVersion >= "4.10.8.0"
Message "opsi-client-agent update"
ShowBitmap "%scriptpath%\opsi.png" "opsi-client-agent"


DefVar $INST_BaseDir$
DefVar $INST_NotifierDir$
DefVar $INST_OpsiclientdDir$
DefVar $INST_OpsiclientdConf$
DefVar $INST_WinstDir$
DefVar $OLB_LoginBlockerStart$
DefVar $block_login$
DefVar $winst_skin_color$
DefVar $action_color$
DefVar $count_event_abort$
DefVar $warning_before_event_sec$

Set $INST_BaseDir$   = "%ProgramFilesDir%\opsi.org\opsi-client-agent"
Set $INST_OpsiclientdDir$   = $INST_BaseDir$+"\opsiclientd"
Set $INST_OpsiclientdConf$ = $INST_OpsiclientdDir$+"\opsiclientd.conf"
Set $INST_WinstDir$   = $INST_BaseDir$+"\opsi-winst"
Set $INST_NotifierDir$ = $INST_BaseDir$+"\notifier"
;set $winst_skin_color$ = "$00E7E7E7"
set $winst_skin_color$ = "$00202020"
set $action_color$ = "32,32,32"
;Set $OLB_LoginBlockerStart$          	    = GetProductProperty ("LoginBlockerStart","off")
set $count_event_abort$ = "2"
set $warning_before_event_sec$ = "20"

;if $OLB_LoginBlockerStart$ = "on"
;	set $block_login$ = "true"
;else
	set $block_login$ = "false"
;endif

;	if FileExists("%ScriptPath%\custom.ins")
;		comment "Start uninstall sub section"
;		Sub "%ScriptPath%\custom.ins"
;	endif

Files_copy_images
Patches_event_ini $INST_NotifierDir$+"\event.ini"
Patches_action_ini $INST_NotifierDir$+"\action.ini"
Patches_shutdown_ini $INST_NotifierDir$+"\shutdown.ini"
Patches_winst_skin $INST_WinstDir$+"\winstskin\skin.ini"
Patches_opsiclientd_conf $INST_OpsiclientdConf$
 
[Files_copy_images]
copy "%SCRIPTPATH%\notifier\*.*" "$INST_NotifierDir$"
copy "%SCRIPTPATH%\winstskin\*.*" "$INST_WinstDir$\winstskin"

[Patches_event_ini]
set [Form] Hidden = false
delsec [ProgressBarCurrentProgress]
delsec [ProgressBarOverallProgress]
set [LabelTitle] FontColor = $action_color$
set [LabelOpsiclientdInfo] FontColor = $action_color$
set [LabelActionProcessorInfo] FontColor = $action_color$
set [LabelStatus] FontColor = $action_color$
set [LabelDetail] FontColor = $action_color$
set [LabelConfigServiceUrl] FontColor = $action_color$
set [LabelConfigServiceUrl] Left = 30
set [LabelConfigServiceUrl] Top = 80
set [LabelClientId] FontColor = $action_color$
set [LabelClientId] Left = 30
set [LabelClientId] Top = 95
set [LabelClientId] Alignment = left
set [LabelDetail] Left = 30
set [LabelDetail] Top = 370
set [LabelDetail] Alignment = left

[Patches_action_ini]
set [LabelMessage] FontSize = 12
set [LabelMessage] FontBold = true
set [LabelMessage] Top = 95
set [LabelMessage] FontBold = true
set [LabelStatus] Left = 25
set [LabelStatus] Top = 55

[Patches_shutdown_ini]
set [LabelMessage] FontSize = 12
set [LabelMessage] FontBold = true
set [LabelMessage] Top = 95
set [LabelMessage] FontBold = true
set [LabelStatus] Left = 25
set [LabelStatus] Top = 55

[Patches_opsiclientd_conf]
set [event_gui_startup] event_notifier_desktop = winlogon
set [event_gui_startup] action_processor_desktop = winlogon
set [event_gui_startup] action_user_cancelable = $count_event_abort$
set [event_gui_startup] block_login = $block_login$
set [event_gui_startup] action_warning_time = $warning_before_event_sec$
set [event_gui_startup] shutdown_user_cancelable = 2
set [event_gui_startup] shutdown_warning_time = 300
set [event_gui_startup{user_logged_in}] event_notifier_desktop = winlogon
set [event_gui_startup{user_logged_in}] action_processor_desktop = winlogon
set [event_gui_startup{user_logged_in}] action_user_cancelable = $count_event_abort$
set [event_gui_startup{user_logged_in}] action_warning_time = $warning_before_event_sec$
set [event_gui_startup{user_logged_in}] shutdown_user_cancelable = 2

[Patches_winst_skin]
set [LabelVersion] FontColor = $winst_skin_color$
set [LabelProduct] FontColor = $winst_skin_color$
set [LabelProduct] FontSize = 22
set [LabelInfo] FontColor = $winst_skin_color$
set [LabelDetail] FontColor = $winst_skin_color$
set [LabelCommand] FontColor = $winst_skin_color$
set [LabelProgress] FontColor = $winst_skin_color$
set [ProgressBar] BarColor = $winst_skin_color$
set [ProgressBar] StartColor = $winst_skin_color$
set [ProgressBar] FinalColor = $winst_skin_color$
set [ProgressBar] ShapeColor = $winst_skin_color$
set [Form] Color = $00E7E7E7
----
